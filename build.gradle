
/**
 *   A slightly complicated build script for several reasons. 
 *   
 *   The main complexity is that javascript files are placed into a target 
 *   directory structure within a base directory that changes name with every 
 *   build. This helps with development because it counters browsers that cache
 *   Javascript files agressively and fail to check datestamps.
 *   
 *   There is also a complication due to two dependencies not being in
 *   Maven Central.
 *   
 **/


/* This class is used to auto edit refs to Javascript in JSP and HTML. */
import org.apache.tools.ant.filters.ReplaceTokens


apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'war'
apply plugin: 'maven-publish'

/**
 * Setting values for these two standard variables
 **/
description = 'LBU-LTI-Tools'
group = 'uk.ac.leedsbeckett'

/** A custom defined variable which changes on every build **/
def buildTimestamp      = Long.toHexString( new Date().getTime() )

/**
 * These new variables relate to the process of assembling files into 
 * two intermediate directories which will later be joined together
 * into the war file.
 **/
def buildWebappStatic   = project.buildDir.toString() + '/webappstatic'
def buildWebappDynamic  = project.buildDir.toString() + '/webappdynamic'
def buildJavascriptDir  = buildWebappDynamic + '/javascript/' + buildTimestamp
def buildJavascriptGen  = buildJavascriptDir + '/generated'

/**
 * This is here because the ToolApi dependency may be a snapshot version.
 * Changing the resolution strategy means the ToolApi will be updated even
 * when the snapshot version hasn't changed.
 **/
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


/** More detailed warnings from compiler. **/
compileJava {
    options.compilerArgs << '-Xlint:unchecked' 
}

/** Make sure code runs on Java 11 even though compiler is more recent. **/
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}


/** Custom tasks for building webapp. **/

/** 
 * Copy source webapp to a holding directory, filtering out the files
 * that will need processing (HTML, JSP) or relocating (JS).
 **/
task staticWebappMove( type: Copy ) {
  from 'src/main/webapp'
  into buildWebappStatic
  exclude '**/*.js'
  exclude '**/*.jsp'
  exclude '**/*.html'
  includeEmptyDirs = false
}

/**
 * Relocate source javascript into the build specific directory.
 * (But delete the entire dynamic directory first.)
 **/
task moveJavascript( type: Copy ) {
  from 'src/main/webapp/javascript'
  into buildJavascriptDir
  include "**/*.js"
  doFirst {
    delete buildWebappDynamic
  }
}

/**
 * Prepare the HTML files that need to point at Javascript. (After calling the
 * move javascript task, which itself deletes the whole build directory structure.)
 * Then source HTML is run through filter to fix links to scripts.
 **/
task processPages( type: Copy ) {
  dependsOn moveJavascript
  from 'src/main/webapp'
  into buildWebappDynamic
  include "**/*.html"
  include "**/*.jsp"
  includeEmptyDirs = false
  filter( ReplaceTokens, tokens: [ "BUILDTIMESTAMP" : buildTimestamp ] )
}

/**
 * Build javascript libraries that will help programmers access the WebSocket
 * endpoints on the server from the web pages. These will be placed into a
 * subfolder of the webapp.
*/
task generateJavascript( type: JavaExec ) {
  dependsOn processPages, compileJava
  classpath = sourceSets.main.runtimeClasspath
  mainClass = "uk.ac.leedsbeckett.ltitoolset.websocket.EndpointJavascriptGenerator"
  args 'uk.ac.leedsbeckett', buildJavascriptGen
}


/* No longer precompiling JSP - it is not necessary with current Tomcat config */


/**
 Defining this do-nothing task stops NetBeans injecting a task of the same name
 that messes up task dependencies and can't be configured not to.
**/
task explodedWar( type: DefaultTask )
{
}


/**
  *  The standard war task (provided by the war plugin) is customised
  *  because we build the webapp files differently. There are two 'dependsOn'.
  *  One processes webapp source files that don't need to be edited, the
  *  second HTML and JSP that needs to be processed. 
  *  
  *  The main source of web files is changed from the normal source folder
  *  to the static directory containing a subset of files; the directory
  *  containing the processed HTML files is added with a 'from' statement;
  */
war {
  dependsOn generateJavascript, staticWebappMove
  webAppDirectory = file( buildWebappStatic )
  from buildWebappDynamic
}


/**
 * Maven central is where all 3rd party libraries come from.
 * Maven local will be used for snapshot builds of our own libraries.
 * Non-snapshot versions of our own libraries will be on Github which
 * means authentication is required. The builders own credentials need
 * to be defined in the builders own personal gradle configuration. E.g.
 * by setting variables in ~/.gradle/gradle.properties
**/
repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // There are two dependencies from leedsbeckett - the basic LTI library
    // and the toolapi library.
    implementation( 'io.github.leeds-beckett-digital-learning:lbu-lti:0.1.6' )
    // { changing = true }
    
    // If using a snapshot of toolapi, set changing to true otherwise set it to 
    // false. This makes it possible to rebuild toolapi repeatedly with the
    // same version no. and publish to local repo. This project will refetch it 
    // from local maven repository every time. For
    // non-snapshot versions, the build will be speeded up by fetching
    // the library from remote repo. only if an official new version is referenced.
    implementation( 'io.github.leeds-beckett-digital-learning:lbu-lti-toolapi:0.1.12' )
    // { changing = true }
  
    providedCompile 'javax.servlet:javax.servlet-api:4.0.0'
    providedCompile 'javax.websocket:javax.websocket-api:1.1'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.apache.commons:commons-text:1.12.0'
    // implementation 'org.apache.httpcomponents.core5:httpcore5:5.1.2'
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'org.ehcache:ehcache:3.10.8'
    // For logging from ehcache
    implementation 'ch.qos.logback:logback-classic:1.2.13'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
    implementation 'jstl:jstl:1.2'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    // This line enables use of RSASSA-PSS (PS256, PS384, PS512) algorithms:
    runtimeOnly 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    implementation "com.sun.mail:javax.mail:1.6.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
}


/**
 *  Set up to publish into github. Requires credentials to be set in builder's
 *  own gradle configuration.
**/
publishing {
  publications {
    mavenWeb(MavenPublication) {
      from components.web
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-lti-tools"
      credentials {
        username = gitHubUserName
        password = gitHubPrivateToken
      }
    }
  }
}
