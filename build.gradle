apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'war'
apply plugin: 'maven-publish'

import org.apache.tools.ant.filters.ReplaceTokens

def buildWebappStatic   = project.buildDir.toString() + '/webappstatic'
def buildWebappDynamic  = project.buildDir.toString() + '/webappdynamic'
def buildTimestamp      = Long.toHexString( new Date().getTime() )
def buildJavascriptDir  = buildWebappDynamic + '/javascript/' + buildTimestamp
def buildJavascriptGen  = buildJavascriptDir + '/generated'

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked' 
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

task staticWebappMove( type: Copy ) {
  from 'src/main/webapp'
  into buildWebappStatic
  exclude '**/*.js'
  exclude '**/*.jsp'
  exclude '**/*.html'
  includeEmptyDirs = false
}


task moveJavascript( type: Copy ) {
  from 'src/main/webapp/javascript'
  into buildJavascriptDir
  include "**/*.js"
  doFirst {
    delete buildWebappDynamic
  }
}

task processPages( type: Copy ) {
  dependsOn moveJavascript
  from 'src/main/webapp'
  into buildWebappDynamic
  include "**/*.jsp"
  include "**/*.html"
  includeEmptyDirs = false
  filter( ReplaceTokens, tokens: [ "BUILDTIMESTAMP" : buildTimestamp ] )
}


task generateJavascript( type: JavaExec ) {
  dependsOn processPages
  classpath = sourceSets.main.runtimeClasspath
  mainClass = "uk.ac.leedsbeckett.ltitoolset.websocket.EndpointJavascriptGenerator"
  args 'uk.ac.leedsbeckett', buildJavascriptGen
}



war {
  dependsOn generateJavascript, staticWebappMove
  webAppDirectory = file( buildWebappStatic )
  from buildWebappDynamic
}

description = 'LBU-LTI-Tools'
    group = 'uk.ac.leedsbeckett'
repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-lti"
      credentials {
        username = gitHubUserName
        password = gitHubPrivateToken
      }
    }
    maven {
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-lti-toolapi"
      credentials {
        username = gitHubUserName
        password = gitHubPrivateToken
      }
    }
}

dependencies {
    // providedCompile 'javax:javaee-web-api:7.0'
    providedCompile 'javax.servlet:javax.servlet-api:4.0.0'
    providedCompile 'javax.websocket:javax.websocket-api:1.1'

    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-text:1.9'
    // implementation 'org.apache.httpcomponents.core5:httpcore5:5.1.2'
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'org.ehcache:ehcache:3.10.0'

    // For logging from ehcache
    implementation 'ch.qos.logback:logback-classic:1.2.11'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.0'
    
    implementation 'jstl:jstl:1.2'
    implementation 'uk.ac.leedsbeckett:lbu-lti:0.0.22'
    //implementation 'uk.ac.leedsbeckett:lbu-lti-toolapi:0.0.7'
    implementation changing: true, group: 'uk.ac.leedsbeckett', name: 'lbu-lti-toolapi', version: '0.0.8-SNAPSHOT'

    implementation 'org.reflections:reflections:0.10.2'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2',
            // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
            'org.bouncycastle:bcprov-jdk15on:1.60',
            'io.jsonwebtoken:jjwt-jackson:0.11.2'

    testImplementation     'junit:junit:4.13'
}

publishing {
  publications {
    mavenWeb(MavenPublication) {
      from components.web
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-lti-tools"
      credentials {
        username = gitHubUserName
        password = gitHubPrivateToken
      }
    }
  }
}